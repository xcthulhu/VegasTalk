---
title: "Limits And Colimits in Haskell"
author: [Matthew Doty]
date: "2019-04-10"
subject: "Haskell"
keywords: [Haskell, Category Theory, Limits, Adjunctions]
lang: "en"
listings-disable-line-numbers: true
...

# Limits In Haskell

## Limits

Today I want to talk  about _limits_ and _colimits_ in Haskell.

Limits and colimits are objects in category theory.  They are defined in terms of functors. The full definition is a bit abstract.  I will start with an simple definition of a limit in Haskell due to Ed Kmett.  The original source can be found in [`category-extras`][1].

The mathematical notation for representing a limit for a functor $F$ is:

$$ \underset{\longrightarrow}{\lim}\;F $$

In Haskell a _limit_ is a universally quantified type constructor.  Arbitrary type constructors are used instead of functors in Haskell. This is because they make for simpler definitions. Here is a way to define a limit using a `newtype`:

```haskell
newtype Limit f = Limit { unLimit :: forall a. f a }
```

A _continuous_ functor is a functor that preserves limits.  This is like a continuous _function_ from calculus.  In calculus, a function is continuous whenever

$$ f \left( \lim_{i \to \infty} x_i \right) = \lim_{i \to \infty} f ( x_i ) $$

In category theory a functor $F$ is continuous if and only if:

$$ F \left(\underset{\longrightarrow}{\lim}\;G\right) \cong \underset{\longrightarrow}{\lim}(F \cdot G) $$

Here $F \cdot G$ denotes functor composition.  Haskell defines type constructor composition in the [`GHC.Generics`][2] module in `base`:

```haskell
infixr 7 :.:
newtype (:.:) f g p = Comp1 { unComp1 :: f (g p) }
```

A continuous type constructor has a pair of isomorphisms associated with it:

```haskell
class Continuous f where
  composeLimit   :: f (Limit g)     -> Limit (f :.: g)
  decomposeLimit :: Limit (f :.: g) -> f (Limit g)
```

One example of a continuous functor is the reader monad `(->) r`.

```haskell
instance Continuous ((->) r) where
  composeLimit r2LimitG =
    Limit (Comp1 (unLimit . r2LimitG))
  decomposeLimit limit r =
    Limit $ (unComp1 (unLimit limit)) r
```

There is a general reason the reader monad is continuous.  It follows from the theory of _adjoint functors_.

## Adjunctions

_Adjunctions_ are fundamental relationships in category theory.  Let [$\hom(A,B)$][3] denote the _hom-set_ of morphisms between objects $A$ and $B$ in a category. Two functors $F$ and $G$ are adjoint if and only if, for all objects $X$ and $Y$:

$$hom(F X, Y) \cong hom(X, G Y)$$

In this case we say $F$ is the _left_ adjoint and $G$ is the _right_ adjoint.  In mathematical shorthand denotes this relationship with $F \dashv G$.

In Haskell there is an _internal_ hom-set.  It is the function arrow `(->)`.  Following [`Data.Functor.Adjunction`][4], adjunctions are defined in Haskell by:

```haskell
class Adjunction f u | f -> u, u -> f where
  leftAdjunct  :: (f a -> b) -> a -> u b
  rightAdjunct :: (a -> u b) -> f a -> b
```

One adjunction in Haskell is between the _product comonad_ `(,) r` and the _reader monad_ `(->) r`:

```haskell
instance Adjunction ((,) r) ((->) r) where
  leftAdjunct  = flip . curry
  rightAdjunct = uncurry . flip
```

This adjunction is key to why `(->) r` is continuous.  In order to see this it is helpful to understand some properties of adjunctions.

## Uniqueness of Adjunctions

Adjoints are _unique up to isomorphism_.  For right adjoints this means that if $F \dashv G$ and $F \dashv H$ then $G X \cong H X$ for all $X$. Left adjoints are similar.  In Haskell this is expressed by constructing a pair of _natural transformations_.  The [`indexed`][5] module defines natural transformations as:

```haskell
infixr 0 ~>
type f ~> g = forall x. f x -> g x
```

Type constructors `f` and `g` are isomorphic if and only if there are two functions `from :: f ~> g` and `via :: g ~> f`.  This relationship is captured in the `Isomorphism` data type:

```haskell
data Isomorphism f g = Isomorphism
  { via  :: f ~> g
  , from :: g ~> f
  }
```

Using common left and right adjoints there are two rules for introducing an `Isomorphism`:

```haskell
rightAdjunctionsIso :: (Adjunction f u, Adjunction f v) => Isomorphism u v
rightAdjunctionsIso = Isomorphism  { via = nat , from = nat } where
  nat :: (Adjunction f u, Adjunction f v) => u ~> v
  nat = leftAdjunct $ rightAdjunct id

leftAdjunctionsIso :: (Adjunction f u, Adjunction g u) => Isomorphism f g
leftAdjunctionsIso = Isomorphism { via = nat, from = nat } where
  nat :: (Adjunction f u, Adjunction g u) => f ~> g
  nat = rightAdjunct $ leftAdjunct id
```

## Properties of Adjunctions

Uniqueness up to isomorphism helps in expressing adjunction properties in Haskell. For instance if $F \dashv G$ then both $F$ and $G$ must be functors.  Trying to write this with `instance Adjunction f u => Functor u where ...` quickly leads to overlapping instances and undecidable instances.  Instead it is easier to introduce two newtypes with _phantom_ parameters:

```haskell
newtype RightAdjoint f u a = RightAdjoint { unRightAdjoint :: u a }

newtype LeftAdjoint f u a = LeftAdjoint { unLeftAdjoint :: f a }
```

The type constructors `f` and `u` in an instance of `Adjunction f u` must be `Functor`.  This is why in the [`adjunctions`](http://hackage.haskell.org/package/adjunctions) Haskell library the `Adjunction` class has additional type class dependencies. `Functor` instances can be provided using the wrapper classes:

```haskell
unit :: Adjunction f u => a -> u (f a)
unit   = leftAdjunct id

counit :: Adjunction f u => f (u a) -> a
counit = rightAdjunct id

instance Adjunction f u => Functor (RightAdjoint f u) where
  fmap f = RightAdjoint
         . leftAdjunct (f . counit)
         . unRightAdjoint

instance Adjunction f u => Functor (LeftAdjoint f u) where
  fmap f = LeftAdjoint
         . rightAdjunct (unit . f)
         . unLeftAdjoint
```


Adjunctions have

A colimit has similar notation:

$$ \underset{\longleftarrow}{\lim}\;F $$

The dual _colimit_ is an existential quantifier:

```haskell
data Colimit f = forall b. Colimit (f b)
```

The dual notion of _cocontinuous_ is similar.  A functor $F$ is cocontinuous if and only if:

$$ F \left(\underset{\longleftarrow}{\lim}\;G\right) \cong \underset{\longleftarrow}{\lim}(F \cdot G) $$

In Haskell this is represented by another isomorphism:

```haskell
class Cocontinuous f where
  composeColimit   :: f (Colimit g)   -> Colimit (f :.: g)
  decomposeColimit :: Colimit (f :.: g) -> f (Limit g)
```


\begin{tikzpicture}

\def \n {5}
\def \radius {3cm}
\def \margin {8} % margin in angles, depends on the radius

\foreach \s in {1,...,\n}
{
  \node[draw, circle] at ({360/\n * (\s - 1)}:\radius) {$\s$};
  \draw[->, >=latex] ({360/\n * (\s - 1)+\margin}:\radius)
    arc ({360/\n * (\s - 1)+\margin}:{360/\n * (\s)-\margin}:\radius);
}
\end{tikzpicture}

[1]: https://github.com/ekmett/category-extras/blob/6347b17608539936de73ee2cc1e997b3e8396a31/Control/Functor/Limit.hs#L24-L45
[2]: https://github.com/ghc/packages-base/blob/52c0b09036c36f1ed928663abb2f295fd36a88bb/GHC/Generics.hs#L611-L614
[3]: https://ncatlab.org/nlab/show/hom-set
[4]: https://github.com/ekmett/adjunctions/blob/af0b615ddae935feaae81a246c1003d2668a1a0d/src/Data/Functor/Adjunction.hs#L92-L105
[5]: https://github.com/ekmett/indexed/blob/331b5dd12eee9dfa89d8bf2dda18dce04030167b/src/Indexed/Types.hs#L51-L73

[//]: # (
```haskell
main = pure ()
```
Local Variables:
eval: (projectile-mode)
compile-command: make blog.html
End:
)
